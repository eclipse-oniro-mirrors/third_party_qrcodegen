# Copyright (c) Huawei Technologies Co., Ltd. 2020. All rights reserved.

declare_args() {
  qrcodegen_feature_ace_engine_qrcode_able = true
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
} else {
  import("//build/ohos.gni")
}

config("libqrcodegen_config") {
  include_dirs = [ "cpp" ]
}

config("qrcodegen_config") {
  cflags = [
    "-Wall",
    "-fexceptions",
  ]
  cflags_cc = cflags
}

if (defined(ohos_lite)) {
  lite_library("qrcodegen") {
    if (ohos_kernel_type == "liteos_m") {
      target_type = "static_library"
    } else {
      target_type = "shared_library"
    }
    sources = [ "cpp/qrcodegen.cpp" ]
    include_dirs = [ "//third_party/qrcodegen/cpp" ]
    if (defined(board_toolchain_type) && board_toolchain_type == "iccarm") {
      cflags = [
        "--diag_suppress",
        "Pe366",
      ]
      cflags_cc = cflags
    } else {
      cflags = [ "-Wall" ]
      cflags_cc = cflags
    }
    public_configs = [ ":libqrcodegen_config" ]
  }

  lite_component("qrcode") {
    features = [ ":qrcodegen" ]
  }
} else {
  ohos_static_library("qrcodegen_static") {
    sources = [ "cpp/qrcodegen.cpp" ]
    include_dirs = [ "//third_party/qrcodegen/cpp" ]
    configs = [ ":qrcodegen_config" ]
    public_configs = [ ":libqrcodegen_config" ]
  }
  if (qrcodegen_feature_ace_engine_qrcode_able) {
    config("ace_engine_qrcode_config") {
      include_dirs = [ "//third_party/qrcodegen/cpp" ]
      defines = [ "ACE_ENGINE_QRCODE_ABLE" ]
      cflags = [
        "-Wall",
        "-Wno-reorder",
      ]
      cflags_cc = cflags
    }

    ohos_static_library("ace_engine_qrcode") {
      sources = [ "cpp/qrcodegen.cpp" ]
      public_configs = [ ":ace_engine_qrcode_config" ]
      subsystem_name = "thirdparty"
      part_name = "qrcodegen"
    }
  }
}
